class Client is subclass of User
/*
	A client is a user that requests rides.
*/
types
	public Status = <Ready> | <Waiting> | <InTransit>;

instance variables
	public rating: rat := 5;
	public status: Status := <Ready>;
	public location: Utils`Location;

operations
  -- Client Constructor
	public Client : String * nat * nat ==> Client
		Client(nm, lat, lon) == (
			name := nm;
			location := mk_Utils`Location(lat, lon, "Portugal");
			return self
		);
		
	--registers the Client in the system
	public registerClient: Client ==> set of Client
		registerClient(c) == (
			Uber`clients := {c} union Uber`clients;
			return Uber`clients
		)	
		pre (c not in set Uber`clients);

	-- Operations that change the Client's state
	public setReady: () ==> ()
		setReady() ==
		status := <Ready>
		pre status = <InTransit> or status = <Waiting>;
		
	public setWaiting: () ==> ()
		setWaiting() ==
			status := <Waiting>
			pre status = <Ready>;
	
	public setInTransit: () ==> ()
		setInTransit() ==
			status := <InTransit>
			pre status = <Waiting>;
			
end Client