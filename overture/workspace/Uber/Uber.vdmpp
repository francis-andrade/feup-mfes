class Uber

types
    -- Redefinition of Util types for ease of reading and writing
    public Timestamp = UberUtils`Timestamp;
    public Location = UberUtils`Location;
    -- A Report is a document with details about a client's trips
    public Report :: timeStart: UberUtils`Timestamp
                     timeEnd: UberUtils`Timestamp
                     locationStart: UberUtils`Location
                     locationEnd: UberUtils`Location
                     cost:UberUtils`Cost;

instance variables
    -- sets of different Users that use the system.
    public drivers: set of Driver := {};
    public clients: set of Client := {};
    public admins: set of Admin := {};

operations
    -- class constructor
    public Uber:() ==> Uber
        Uber() ==
        return self;
		
		-- add Admin
		public addAdmin: Admin ==> set of Admin
			addAdmin(a) == (
				admins := {a} union admins; 
				return admins;
			);
		
		/*ADMIN OPERATIONS*/
    --registers a Driver in the system
    public registerDriver: Admin * Driver ==> set of Driver
        registerDriver(a, d) == (
            drivers := {d} union drivers;
            return drivers
        )    
        pre (a in set admins and d not in set drivers);
    
    --removes a Driver from the system
    public removeDriver: Admin * Driver ==> set of Driver
        removeDriver(a, d) == (
            drivers := {d} \ drivers;
            return drivers
        )    
        pre (a in set admins and d in set drivers);

    --removes a Client from the system
    public removeClient: Admin * Client ==> set of Client
        removeClient(a, c) == (
            clients := {c} \ clients;
            return clients
        )    
        pre (a in set admins and c in set clients);
    
    /*CLIENT OPERATIONS*/
    --registers a Client in the system
    public registerClient: Client ==> set of Client
        registerClient(c) == (
            clients := {c} union clients;
            return clients
        )    
        pre (c not in set clients);
    
    -- a client requests a ride    
    public requestRide: Client ==> ()
        requestRide(c) == (
            c.setWaiting();
        )
        pre (exists d in set drivers & d.status = <Ready> and c in set clients and c.status = <Ready>)
        post (c.status = <Waiting>);
        
    -- a client cancels a ride 
    public cancelRide: Client ==> ()
    		cancelRide(c) == (
    				c.setReady();
    		)
    		pre (c.status = <Waiting> and c in set clients)
        post (c.status = <Ready>);
        
    -- a client enters the car and goes on a ride
    public goOnRide: Client * Location * Timestamp * Driver ==> ()
        goOnRide(c, destination, initTime, d)==(
            dcl distance:rat, cost:rat, endTime:Timestamp, time:nat;
            
            --c.status := <InTransit>;
            distance := UberUtils`calculateDistance(c.location, destination);
            time := UberUtils`calculateTravelTime(d.speed, distance);
            cost := UberUtils`calculateCost(distance, time);
            endTime:=UberUtils`calculateTimestamp(UberUtils`calculateEndTime(initTime, time));
            c.endTrip(mk_Report(initTime, endTime, c.location, destination, cost));
        ) pre (c.status = <Waiting> and c in set clients) 
        post (c.status = <Ready> and c.location = destination);
    
    /*DRIVER OPERATIONS*/
    -- a driver accepts a ride
    public acceptRide: Driver * Client ==>()
        acceptRide(d, c) == (
            d.setReachingClient();
        )    
        pre (c in set clients and c.status = <Waiting> and d in set drivers and d.status = <Ready>)
        post (d.status=<ReachingClient> and d.location = c.location); 
    
    -- a driver picks up a client and goes to their destination
    public takeOnRide: Driver * Location ==> ()
        takeOnRide(d, destination) == (
        	d.endTrip(destination);
        )
        pre (d.status = <ReachingClient> and d in set drivers)
        post (d.status = <Ready> and d.location = destination);
    
end Uber